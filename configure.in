# Initial blurb
AC_PREREQ(2.52)
AC_INIT(devilspie, 0.22, http://www.burtonini.com/)
AC_CONFIG_SRCDIR(src/devilspie.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(src/config.h)

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

# Do the gettext/i18n stuff
GETTEXT_PACKAGE=devilspie
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [The name of the gettext package.])
AC_SUBST(GETTEXT_PACKAGE)

AC_PROG_CC
AM_PROG_CC_STDC

GNOME_COMPILE_WARNINGS(yes)

AC_PROG_INTLTOOL([0.20])
ALL_LINGUAS="de"
AM_GLIB_GNU_GETTEXT

# Check that we actually have the libraries required to build
PKG_CHECK_MODULES(WNCK,
        glib-2.0 >= 2.9.1
        gdk-2.0
        gdk-x11-2.0
        x11
        libwnck-1.0 >= 0.17)
AC_SUBST(WNCK_CFLAGS)
AC_SUBST(WNCK_LIBS)

# See if we have new libwnck with timestamp arguments
PKG_CHECK_MODULES(WNCK_VER, libwnck-1.0 >= 2.9.92,
		 wnck_new="yes", wnck_new="no")
if test "x$wnck_new" = "xyes"; then
        AC_DEFINE(NEED_TIMESTAMPS,1,[Define to 1 if timestamps are needed to wnck functions])
fi

# Check if we have the set_fullscreen function
AC_CHECK_LIB(wnck-1, wnck_window_set_fullscreen,
            have_fullscreen=1, have_fullscreen=0)
AC_DEFINE_UNQUOTED(HAVE_FULLSCREEN, $have_fullscreen, [set_fullscreen is available])

AC_CHECK_LIB(wnck-1, wnck_window_set_window_type,
            have_set_wintype=1, have_set_wintype=0)
AC_DEFINE_UNQUOTED(HAVE_SET_WINDOW_TYPE, $have_set_wintype, [set_window_type is available])

# Check for the POSIX regexpr functions and headers
AC_CHECK_FUNCS([regcomp regexec regfree])
AC_CHECK_HEADERS([sys/types.h regex.h])

AC_OUTPUT([
devilspie.spec
Makefile
src/Makefile
po/Makefile.in
tests/Makefile
])
